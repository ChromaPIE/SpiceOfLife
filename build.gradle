buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "14.21.0.2340"
    runDir = "run"
    mappings = "snapshot_20170620"

    replace "\${version}", project.version
    replaceIn "ModInfo.java"
}

group = project.projectDir.name.toLowerCase()
archivesBaseName = project.projectDir.name + "-mc" + project.minecraft.version

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.java.srcDirs += 'EvalEx/src'
sourceSets.main.resources.srcDirs += 'resources'
sourceSets.test.java.srcDirs += 'tests'

repositories {
    maven {
        url "http://www.ryanliptak.com/maven/"
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    deobfCompile "applecore:AppleCore:1.12-2.2.1+263.7303c:deobf"
}

task copyEvalExLicense(type:Copy){
    from('EvalEx')
    {
        include 'LICENSE'
    }
    into sourceSets.main.output.resourcesDir.getPath()+"/com/udojava/evalex"
}
processResources.dependsOn copyEvalExLicense

processResources {
    inputs.property "vars", project.version + project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include 'example/**.json'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude 'example/**.json'
    }
}